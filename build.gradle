buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://plugins.gradle.org/m2' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.matthewprenger.cursegradle'

version = '0.2.0'
group = 'io.github.yamporg.darkness'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    mappings = 'stable_39'
    coreMod = 'io.github.yamporg.darkness.DarknessLoadingPlugin'
    runDir = 'run'

    replace '@VERSION@', project.version
}

jar.manifest.attributes(
        'FMLCorePluginContainsFMLMod': true,
        'FMLCorePlugin': 'io.github.yamporg.DarknessLoadingPlugin',
        'ForceLoadAsMod': true,
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'MixinConfigs': 'darkness.mixins.json',
)

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
}

dependencies {
    compile 'org.spongepowered:mixin:0.8.1-SNAPSHOT'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}
tasks.shadowJar.finalizedBy reobfShadowJar
tasks.jar.finalizedBy reobfJar

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

curseforge {
    if (project.hasProperty('curseForgeApiKey')) {
        apiKey = project.curseForgeApiKey
    }
    project {
        id = '363102'
        releaseType = 'release'

        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')

        mainArtifact shadowJar
        addArtifact sourceJar
        addArtifact jar
    }
}
