buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://plugins.gradle.org/m2' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:4.0.13'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.matthewprenger.cursegradle'

// -------------------------------------------------------------------------- //

group = 'io.github.yamporg.darkness'
version = '0.3.0'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

// For some reason resources are not loaded in runClient task
// unless we put them to class dir directly. Newer version of
// Minecraft work just fine though. Thatâ€™s weird.
//
// See https://github.com/MinecraftForge/ForgeGradle/issues/717
// See also https://redd.it/e4hfzz
sourceSets {
   main {
       output.resourcesDir = java.outputDir
   }
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'io.github.yamporg.darkness.DarknessLoadingPlugin'
        }
    }
}

repositories {
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://cursemaven.com' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'
    implementation 'curse.maven:mixinbootstrap-357178:3076610' // MixinBootstrap v1.0.5, Mixin v0.8.2
    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
}

// See https://github.com/MinecraftForge/ForgeGradle/issues/627
configurations.all {
    resolutionStrategy {
        force 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209'
    }
}

// -------------------------------------------------------------------------- //

// Make archive builds reproducible.
//
// See https://docs.gradle.org/4.10.3/userguide/working_with_files.html#sec:reproducible_archives
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

// Set zero timestamps in reobfJar task too.
//
// See https://github.com/MinecraftForge/ForgeGradle/issues/358
// See https://github.com/md-5/SpecialSource/issues/40
tasks.withType(net.minecraftforge.gradle.userdev.tasks.RenameJarInPlace) {
    def xs = ['--stable']
    args.each { xs << it }
    args = xs.toArray([] as String[])

    // And --stable option is available since v1.8.5 while ForgeGradle uses v1.8.3.
    // See https://github.com/md-5/SpecialSource/commit/5dbf0ee84ae02085b4bce7ea9101f16ed6ba8f29
    tool = 'net.md-5:SpecialSource:1.8.6:shaded' 
}

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

jar.manifest.attributes(
    'FMLCorePluginContainsFMLMod': true,
    'FMLCorePlugin': 'io.github.yamporg.darkness.DarknessLoadingPlugin',
)

curseforge {
    if (project.hasProperty('curseForgeApiKey')) {
        apiKey = project.curseForgeApiKey
    }
    project {
        id = '363102'
        releaseType = 'release'

        changelogType = 'markdown'
        changelog = file('CHANGELOG.md')

        mainArtifact(jar) {
            relations {
                requiredDependency 'mixinbootstrap'
            }
        }
        addArtifact sourceJar
    }
}
